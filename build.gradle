plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'groovy'

repositories {
   jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

group = 'com.github.admarple'
version = '1.0.0'

apply plugin: 'maven-publish'
apply plugin: 'java-library'

publishing {
    publications {
        mainJar(MavenPublication) {
            from components.java
        }
    }
}

ext.bintrayUser = hasProperty('bintrayUser') ? bintrayUser : 'no_user_configured'
ext.bintrayKey = hasProperty('bintrayKey') ? bintrayKey : 'no_key_configured'

bintray {
    user = bintrayUser
    key = bintrayKey

    publications = ['mainJar']

    pkg {
        repo = 'gradle'
        name = project.name
        userOrg = 'admarple-org'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/admarple/gradle-plugin-resources'
        version {
            name = project.version
            released  = new Date()
        }
    }

    /*
     * To publish new versions after they've been uploaded:
     *   curl -v -u "$(cat /tmp/bintray_auth_header.txt)" \
     *     -X POST 'https://api.bintray.com/content/admarple-org/gradle/gradle-plugin-resources/1.0.0/publish'
     */
}